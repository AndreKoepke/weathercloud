plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.16.0'
    id 'signing'
}

group = 'io.github.andrekoepke'
version = '0.1.1'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

clean {
    doLast {
        mkdir "$buildDir/jreleaser"
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'

    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.4'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.2'
    implementation 'org.jetbrains:annotations:23.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.slf4j:slf4j-api:1.7.36'
    testImplementation 'org.slf4j:slf4j-simple:1.7.36'

    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.andrekoepke'
            artifactId = 'weathercloud'

            from components.java

            pom {
                name = 'weahtercloud'
                description = 'Pulls data from Weathercloud'
                url = 'https://github.com/AndreKoepke/weathercloud'
                inceptionYear = '2021'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'AndreKoepke'
                        name = 'Andre KÃ¶pke'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/aalmiray/app.git'
                    developerConnection = 'scm:git:ssh://github.com/AndreKoepke/weathercloud.git'
                    url = 'https://github.com/AndreKoepke/weathercloud'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
